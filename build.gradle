apply plugin: "java"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "application"

mainClassName = "bpp.BPP"

repositories {
	mavenCentral()
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'src/main/generated'
		}
	}
	test {
		java {
			srcDir 'src/test/java'
		}
	}
}

dependencies {
	compile 'net.sf.saxon:Saxon-HE:9.5.+'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.1'
}

run {
	if(project.hasProperty('args')){
		args project.args.split('\\s+')
	}
}

task profile (type: JavaExec, dependsOn: build){
    description = 'Builds the test assertion document'
    classpath = sourceSets.main.runtimeClasspath
    main = "bpp.util.WSIProfileDocSerializer"
}

task latex (type: JavaExec, dependsOn: build){
    description = 'Builds LaTeX tables for all test assertions'
    classpath = sourceSets.main.runtimeClasspath
    main = "bpp.util.LatexSerializer"
}

jar {
   exclude('testfiles')
   exclude('mappings')
   exclude('language-features')
   exclude('ProfileDoc-2.0.xsd')
   exclude('ProfileMarkup-2.0.xsd')
   exclude('wsi-report.xsd')
   exclude('xml.xsd')
   exclude('bpp/executables/FileAnalyzerTests*')
   exclude('bpp/testrunner/TestRunner*')
   manifest { attributes 'Main-Class': mainClassName }
}
